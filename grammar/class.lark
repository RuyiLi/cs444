// https://docs.oracle.com/javase/specs/jls/se7/html/jls-18.html

SEMICOLON: ";"

MODIFIER: PUBLIC_KW | PROTECTED_KW | STATIC_KW | FINAL_KW | ABSTRACT_KW | NATIVE_KW

class_declaration.1: MODIFIER+ CLASS_KW IDENTIFIER (EXTENDS_KW class_type)? (IMPLEMENTS_KW interface_type_list)? class_body
interface_type_list: interface_type ("," interface_type)*

class_body: "{" class_body_declaration* "}"

?class_body_declaration: member_declaration
                       | constructor_declaration
                    // | STATIC_KW? block (No initializer block in Joos 1W)

?member_declaration: SEMICOLON
                   | method_declaration
                   | field_declaration
                // | class_declaration
                // | interface_declaration

method_declaration: MODIFIER+ (type | VOID_KW) method_declarator method_body

field_declaration: MODIFIER+ type var_declarator SEMICOLON

method_declarator: IDENTIFIER "(" formal_param_list? ")"
formal_param_list: formal_param ("," formal_param)*
formal_param: type var_declarator_id

method_body: block | SEMICOLON

constructor_declaration: MODIFIER+ type_name "(" formal_param_list? ")" block

interface_declaration: MODIFIER+ INTERFACE_KW IDENTIFIER (EXTENDS_KW interface_type_list)? interface_body
interface_body: "{" interface_body_declaration* "}"
interface_body_declaration: method_declaration -> interface_method_declaration
                          | SEMICOLON
                          // | interface_declaration
                          // | class_declaration
